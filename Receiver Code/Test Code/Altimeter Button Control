#include <Arduino.h>
#include <TFT_eSPI.h>

#include "Hundredths Needle.h" //include bitmap images
#include "Thousandths Needle.h"
#include "Altimeter Background.h"

const int upButtonPin = 2;
const int downButtonPin = 3;
const int leftButtonPin = 4;
const int rightButtonPin = 5;

int altitudeThousandths = 0;
int altitudeHundredths = 0;

TFT_eSPI tft = TFT_eSPI(128, 160);                   /* TFT instance */
TFT_eSprite altimeterBackground = TFT_eSprite(&tft); // create altimeter background sprite
TFT_eSprite needleThousandth = TFT_eSprite(&tft);    // create hundredth needle sprite
TFT_eSprite needleHundredth = TFT_eSprite(&tft);     // create thousandth needle sprite

void createAltimeterBackground()
{
    altimeterBackground.setColorDepth(8);
    altimeterBackground.createSprite(114, 114);
    altimeterBackground.setPivot(57, 57);
    altimeterBackground.fillSprite(TFT_TRANSPARENT);
    altimeterBackground.pushImage(0, 0, 114, 114, altimeterBackgroundImg);
}

void createNeedleHundredth()
{
    needleHundredth.setColorDepth(8);
    needleHundredth.createSprite(8, 60);
    needleHundredth.fillSprite(TFT_TRANSPARENT);
    needleHundredth.pushImage(0, 0, 8, 60, altHandHundredths);
    needleHundredth.setPivot(4, 40);
}

void createNeedleThousandth()
{
    needleThousandth.setColorDepth(8);
    needleThousandth.createSprite(12, 46);
    needleThousandth.fillSprite(TFT_TRANSPARENT);
    needleThousandth.pushImage(0, 0, 12, 46, altHandThousandths);
    needleThousandth.setPivot(6, 30);
}

void drawAltimeter(int16_t thousandthsAngle, int16_t hundredthsAngle)
{
    createAltimeterBackground();
    needleThousandth.pushRotated(&altimeterBackground, thousandthsAngle, TFT_TRANSPARENT);
    needleHundredth.pushRotated(&altimeterBackground, hundredthsAngle, TFT_TRANSPARENT);
    altimeterBackground.pushSprite(0, 14, TFT_TRANSPARENT);
}

void setup()
{
    Serial.begin(9600);
    tft.begin();        /* TFT init */
    tft.setRotation(1); /* set rotation */
    tft.fillScreen(TFT_BLACK);
    createNeedleHundredth();
    createNeedleThousandth();
    pinMode(upButtonPin, INPUT_PULLUP);
    pinMode(downButtonPin, INPUT_PULLUP);
    pinMode(leftButtonPin, INPUT_PULLUP);
    pinMode(rightButtonPin, INPUT_PULLUP);
}

void loop()
{
    if (digitalRead(upButtonPin) == LOW)
    {
        if (altitudeThousandths < 10)
        {
            altitudeThousandths += 1;
            delay(100);
        }
    }
    else if (digitalRead(downButtonPin) == LOW)
    {
        if (altitudeThousandths > 0)
        {
            altitudeThousandths -= 1;
            delay(100);
        }
    }
    float angleMapThousandths = map(altitudeThousandths, 0, 10, 0, 360);

    if (digitalRead(rightButtonPin) == LOW)
    {
        if (altitudeHundredths <= 10)
        {
            altitudeHundredths += 1;
            delay(100);
        }
    }
    else if (digitalRead(leftButtonPin) == LOW)
    {
        if (altitudeHundredths >= 0)
        {
            altitudeHundredths -= 1;
            delay(100);
        }
    }
    float angleMapHundredths = map(altitudeHundredths, 0, 10, 0, 360);
    drawAltimeter(angleMapThousandths, angleMapHundredths);
}
